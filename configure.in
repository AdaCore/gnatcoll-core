# Set the version number below
AC_INIT(gnatcoll,1.5w,report@adacore.com,gnatcoll)
AC_CANONICAL_SYSTEM

AC_PROG_LN_S
AC_FUNC_MMAP

CHECK_BUILD_TYPE
AM_GNAT_BUILDS_SHARED
AM_PATH_GTK
AM_PATH_PYTHON
AM_PATH_POSTGRES
AM_PATH_SQLITE
AM_PATH_PYGTK(2.8)
AM_PATH_GMP
AM_GNAT_SOURCES
AM_PROJECTS
AM_HAS_INTRINSIC_SYNC_COUNTERS
AM_TO_GPR($GMP_CFLAGS,    GMP_CFLAGS_GPR)
AM_TO_GPR($GMP_LIBS,      GMP_LIBS_GPR)
AM_TO_GPR($PYTHON_LIBS,   PYTHON_LIBS_GPR)
AM_TO_GPR($PYTHON_CFLAGS, PYTHON_CFLAGS_GPR)
AM_TO_GPR($PYGTK_INCLUDE, PYGTK_INCLUDE_GPR)
AM_TO_GPR($GTK_GCC_FLAGS, GTK_GCC_FLAGS_GPR)

AM_PATH_SYSLOG

AM_LINK_FILE([src/gnatcoll-refcount-sync_counters__$SYNC_COUNTERS_IMPL.ada],
             [src/gnatcoll-refcount-sync_counters.adb],
             [SYNC_COUNTERS_IMPL=$SYNC_COUNTERS_IMPL])
AM_LINK_FILE([src/gnatcoll__$GNAT_SOURCES.gpr],
             [src/gnatcoll.gpr],
             [GNAT_SOURCES=$GNAT_SOURCES])
AM_LINK_FILE([distrib/gnatcoll.gpr__$GNAT_SOURCES],
             [distrib/gnatcoll.gpr])

AC_OUTPUT(gnatcoll_shared.gpr Makefile.conf src/gnatcoll_gmp.gpr src/gnatcoll_python.gpr distrib/gnatcoll.gpr__gnat_util distrib/gnatcoll.gpr__copy distrib/gnatcoll_python.gpr distrib/gnatcoll_gmp.gpr gnat/sdefault/sdefault.adb)

AC_MSG_NOTICE([--------- Summary for GNAT Components --------------])
AC_MSG_NOTICE([ Shared libraries: $GNAT_BUILDS_SHARED (default: $DEFAULT_LIBRARY_TYPE)])
AC_MSG_NOTICE([ Gtk+:   $WITH_GTK])
AC_MSG_NOTICE([    Make sure pkg-config is in your PATH])
AC_MSG_NOTICE([    and gtkada.gpr in your project path])
AC_MSG_NOTICE([ Python: $WITH_PYTHON $PYTHON_BASE])
AC_MSG_NOTICE([    use --with-python to override])
AC_MSG_NOTICE([ PyGtk:  $WITH_PYGTK $PYGTK_PREFIX])
AC_MSG_NOTICE([    use --enable-pygtk to override])
AC_MSG_NOTICE([ Syslog: $WITH_SYSLOG])
AC_MSG_NOTICE([    use --enable-syslog to override])
AC_MSG_NOTICE([ PostgreSQL: $WITH_POSTGRES $PATH_LIBPQ])
AC_MSG_NOTICE([    use --with-postgresql to override])
AC_MSG_NOTICE([ Sqlite: $WITH_SQLITE $PATH_LIBSQLITE])
AC_MSG_NOTICE([    use --with-sqlite to override])
AC_MSG_NOTICE([ Projects: $WITH_PROJECTS])
if test $WITH_PROJECTS = no; then
AC_MSG_NOTICE([    create gnat_src to override])
fi
AC_MSG_NOTICE([ gmp: $WITH_GMP])
AC_MSG_NOTICE([    use --with-gmp to override])
AC_MSG_NOTICE([--------------------------------------------])

